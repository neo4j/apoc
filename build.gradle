plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id 'maven-publish'
    id "com.github.hierynomus.license-report" version "0.16.1"
    id "com.diffplug.spotless" version "6.22.0"
}

downloadLicenses {
    excludeDependencies = [
            'org.neo4j.*'
    ]
}

allprojects {
    group = 'org.neo4j.procedure'
    version = System.getenv('APOC_VERSION') ? System.getenv('APOC_VERSION') : '5.22.0'
    archivesBaseName = 'apoc'
    description = """neo4j-apoc-procedures"""
}

apply plugin: 'java-library'
if (System.env.CI != null)
    apply from: 'teamcity-repository.gradle'

repositories {

    /*maven {  // this contains the neo4j 4.0.0-beta jars
    url "https://neo4j.bintray.com/community/"
}*/
    if (System.getenv("CUSTOM_REPO_URL") ?: "" != "") {
        maven {
            url System.env.CUSTOM_REPO_URL
            credentials {
                username System.env.CUSTOM_REPO_USERNAME
                password System.env.CUSTOM_REPO_TOKEN
            }
        }
    }
    mavenCentral()
    maven {
        url "https://repo.gradle.org/gradle/libs-releases"
    }
    mavenLocal()
}

subprojects {
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'java-library'

    spotless {
        java {
            target 'src/*/java/**/*.java'
            removeUnusedImports()
            palantirJavaFormat()
        }
    }

    repositories {

        /*maven {  // this contains the neo4j 4.0.0-beta jars
        url "https://neo4j.bintray.com/community/"
    }*/
        if (System.getenv("CUSTOM_REPO_URL") ?: "" != "") {
            maven {
                url System.env.CUSTOM_REPO_URL
                credentials {
                    username System.env.CUSTOM_REPO_USERNAME
                    password System.env.CUSTOM_REPO_TOKEN
                }
            }
        }
        mavenCentral()
        maven {
            url "https://repo.gradle.org/gradle/libs-releases"
        }
        mavenLocal()
    }


    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    task mySourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task myJavadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }
    test {
        //exclude '**/CypherProceduresClusterTest.class'//, '**/AtomicTest.class'

        // neo4jDockerImage system property is used in TestContainerUtil
        systemProperties 'user.language': 'en',
                'user.country': 'US',
                'neo4jDockerImage': project.hasProperty("neo4jDockerEeOverride") ? project.getProperty("neo4jDockerEeOverride") : 'neo4j:5.23.0-enterprise-debian',
                'neo4jCommunityDockerImage': project.hasProperty("neo4jDockerCeOverride") ? project.getProperty("neo4jDockerCeOverride") : 'neo4j:5.23.0-debian',
                'coreDir': 'core',
                'testDockerBundle': project.hasProperty("testDockerBundle") ? true : false

        maxHeapSize = "5G"
        forkEvery = 50
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        minHeapSize = "128m"

        // This would apply only to TeamCity
        // We need to ignore the failures because we may have tests muted
        if (System.env.TEAMCITY_VERSION != null) {
            ignoreFailures(true)
            if (project.hasProperty('excludeSeleniumTests')) {
                exclude '**/LoadHtmlTest*'
                exclude '**/LoadHtmlTestParameterized*'
            }
        }

        jvmArgs = ["--add-opens", "java.base/java.lang=ALL-UNNAMED",
                   "--add-opens", "java.base/java.nio=ALL-UNNAMED",
                   "--add-opens", "java.base/java.io=ALL-UNNAMED",
                   "--add-opens", "java.base/java.util=ALL-UNNAMED",
                   "--add-opens", "java.base/java.util.concurrent=ALL-UNNAMED",
                   "--add-opens", "java.base/sun.net.www.protocol.http=ALL-UNNAMED",
                   "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED",
                   "--add-opens", "jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
                   "--add-opens", "jdk.compiler/com.sun.tools.javac=ALL-UNNAMED",
                   "--add-opens", "jdk.compiler/com.sun.source.doctree=ALL-UNNAMED",
                   "--add-opens", "jdk.compiler/com.sun.source.tree=ALL-UNNAMED",
                   "--add-opens", "jdk.compiler/com.sun.source.util=ALL-UNNAMED"]

        filter {
            setFailOnNoMatchingTests(false)
        }

        testLogging.showStandardStreams = true
    }

    configurations {
        apt
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'io.netty' && !details.requested.name.contains('netty-tcnative') && !details.requested.name.equals('netty')) {
                details.useVersion '4.1.101.Final' // Use same Netty version as server
            }
        }
    }

    compileJava {
        options.annotationProcessorPath = configurations.apt
        options.compilerArgs += ["-AIgnoreContextWarnings"]
        options.encoding = "UTF-8"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

}

apply from: "licenses-3rdparties.gradle"
apply from: "licenses-source-header.gradle"

ext {
    publicDir = "${project.rootDir}"
    neo4jVersionEffective = project.hasProperty("neo4jVersionOverride") ? project.getProperty("neo4jVersionOverride") : "5.23.0"
    testContainersVersion = '1.19.1'
    apacheArrowVersion = '15.0.0'
}
