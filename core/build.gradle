import org.gradle.api.internal.artifacts.DefaultExcludeRule

plugins {
    id 'java'
    id 'maven-publish'
}

archivesBaseName = "apoc"

apply plugin: 'com.github.johnrengelman.shadow'

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
    duplicatesStrategy = 'exclude'
}


configurations {
    apt
}

shadowJar {
    archiveClassifier.set('core')
    duplicatesStrategy = 'exclude'
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}


dependencies {
    apt project(':processor')
    apt group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective

    // These will be dependencies packaged with the .jar
    implementation project(":common")
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1', {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
        exclude group: 'org.apache.commons', module: 'commons-collections4'
    }
    implementation group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.7.17'

    def arrowExclusions = {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'io.netty', module: 'netty-common'
        exclude group: 'io.netty', module: 'netty-buffer'
    }

    // These will be dependencies not packaged with the .jar
    // They need to be provided either through the database or in an extra .jar
    compileOnly group: 'org.apache.arrow', name: 'arrow-vector', version: apacheArrowVersion, arrowExclusions
    compileOnly group: 'org.apache.arrow', name: 'arrow-memory-netty', version: apacheArrowVersion, arrowExclusions

    compileOnly group: 'org.neo4j', name: 'neo4j', version: neo4jVersionEffective
    compileOnly group: 'com.google.guava', name: 'guava', version: '33.0.0-jre'

    // These dependencies affect the tests only, they will not be packaged in the resulting .jar
    testImplementation project(":common").sourceSets.test.output
    testImplementation project(':test-utils')
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testImplementation group: 'org.mock-server', name: 'mockserver-netty', version: '5.15.0', {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    testImplementation 'org.assertj:assertj-core:3.26.3'

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

publishing {
    repositories {
        maven {
            name = 'pipeline'
            url = "file://${project(':core').buildDir}/repo"
        }
        if (System.getenv("CODEARTIFACT_PUBLISH_URL") ?: "" != "") {
            maven {
                name = 'codeartifact-publish'
                url System.getenv('CODEARTIFACT_PUBLISH_URL')
                credentials {
                    username System.getenv('CODEARTIFACT_USERNAME')
                    password System.getenv('CODEARTIFACT_TOKEN')
                }
            }
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            artifactId("apoc-core")
            project.shadow.component(publication)
            artifact(mySourcesJar)
            artifact(myJavadocJar)
            artifact(jar)

            pom.withXml {
                def root = asNode()
                root.appendNode("name", "neo4j-apoc-core")
                root.appendNode("description", "Core package for Neo4j Procedures")
                root.appendNode("url", "http://github.com/neo4j/apoc")

                def scmNode = root.appendNode("scm")
                scmNode.appendNode("url", "http://github.com/neo4j/apoc")

                def licensesNode = root.appendNode("licenses")
                def licenseNode = licensesNode.appendNode("license")
                licenseNode.appendNode("name", 'The Apache Software License, Version 2.0')
                licenseNode.appendNode("url", 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                licenseNode.appendNode("comments", 'Note that this license is for the project itself, and not for its dependencies.')
                licenseNode.appendNode("distribution", 'repo')

                def developersNode = root.appendNode("developers")
                def developerNode = developersNode.appendNode("developer")
                developerNode.appendNode("id", 'neo4j')
                developerNode.appendNode("name", 'The Neo4j Team')
                developerNode.appendNode("url", 'https://neo4j.com/')
                developerNode.appendNode("organization", 'Neo4j Inc.')
                developerNode.appendNode("organizationUrl", 'https://neo4j.com/')

                def dependenciesNode = root.get("dependencies")[0]

                if (dependenciesNode != null) {
                    configurations.default.allDependencies.findAll {dependency -> dependency.group != "org.neo4j.procedure"}.forEach {
                        def dependency = dependenciesNode.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        dependency.appendNode('scope', "compile")

                        def myExcludeRules = it.excludeRules
                        if (myExcludeRules.size() == 0) {
                            myExcludeRules = new HashSet<>()
                        }

                        myExcludeRules.add(new DefaultExcludeRule("org.slf4j", "slf4j-nop"))

                        // for exclusions
                        if (myExcludeRules.size() > 0) {
                            def exclusions = dependency.appendNode('exclusions')
                            myExcludeRules.each { ExcludeRule ex ->
                                def exclusion = exclusions.appendNode('exclusion')
                                exclusion.appendNode('artifactId', ex.module ? ex.module : "*")
                                exclusion.appendNode('groupId', ex.group)
                            }
                        }
                    }
                }
            }
        }
    }
}

task copyRuntimeLibs(type: Copy) {
    into "lib"
    from configurations.testRuntimeClasspath
}